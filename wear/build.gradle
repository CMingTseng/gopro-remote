buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'

def versionMajor = Integer.parseInt(APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(APP_VERSION_MINOR)
def versionPatch = Integer.parseInt(APP_VERSION_PATCH)
def versionBuild = Integer.parseInt(APP_VERSION_BUILD)

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId project.PACKAGE_NAME
        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION_WEAR)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                def suffix = defaultConfig.versionName
                def fileName = file.name.replace(".apk", "-" + suffix + ".apk")
                output.outputFile = new File(file.parent, fileName)
            }
        }

        proguardFiles 'proguard-rules.pro'
        proguardFiles '../proguard/proguard-butterknife.pro'
        proguardFiles '../proguard/proguard-google-play-services.pro'
        proguardFiles '../proguard/proguard-retrolambda.pro'
        proguardFiles '../proguard/proguard-rx-java.pro'
        proguardFiles '../proguard/proguard-square-otto.pro'
        proguardFiles '../proguard/proguard-timber.pro'
        proguardFiles getDefaultProguardFile('proguard-android.txt')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled Boolean.parseBoolean(MINIFY_ENABLED_DEBUG)
            shrinkResources Boolean.parseBoolean(SHRINK_RESOURCES_DEBUG)
        }

        release {
            minifyEnabled Boolean.parseBoolean(MINIFY_ENABLED_RELEASE)
            shrinkResources Boolean.parseBoolean(SHRINK_RESOURCES_RELEASE)
        }
    }
}

dependencies {
    compile project(':common')
    compile 'com.google.android.support:wearable:2.0.0-alpha1'
    provided 'com.google.android.wearable:wearable:+'
    compile "com.google.android.gms:play-services-wearable:${PLAY_SERVICES_VERSION}"

    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'
    compile 'com.jakewharton.timber:timber:4.1.2'

    compile 'com.trello:navi:0.2.0'
    compile 'com.trello:rxlifecycle:0.6.1'
    compile 'com.trello:rxlifecycle-components:0.6.1'
}
