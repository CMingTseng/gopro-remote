repositories {
    mavenLocal()
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'

def versionMajor = Integer.parseInt(APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(APP_VERSION_MINOR)
def versionPatch = Integer.parseInt(APP_VERSION_PATCH)
def versionBuild = Integer.parseInt(APP_VERSION_BUILD)

android {
    compileSdkVersion Integer.parseInt(ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

        applicationId PACKAGE_NAME

        proguardFiles 'proguard-rules.pro'
        proguardFiles '../proguard/proguard-butterknife.pro'
        proguardFiles '../proguard/proguard-gson.pro'
        proguardFiles '../proguard/proguard-google-play-services.pro'
        proguardFiles '../proguard/proguard-retrolambda.pro'
        proguardFiles '../proguard/proguard-rx-java.pro'
        proguardFiles '../proguard/proguard-square-okhttp.pro'
        proguardFiles '../proguard/proguard-square-retrofit.pro'
        proguardFiles '../proguard/proguard-timber.pro'
        proguardFiles getDefaultProguardFile('proguard-android.txt')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled Boolean.parseBoolean(MINIFY_ENABLED_DEBUG)
            shrinkResources Boolean.parseBoolean(SHRINK_RESOURCES_DEBUG)
        }

        release {
            minifyEnabled Boolean.parseBoolean(MINIFY_ENABLED_RELEASE)
            shrinkResources Boolean.parseBoolean(SHRINK_RESOURCES_RELEASE)
        }
    }
}

dependencies {
    wearApp project(':wear')
    compile project(':common')
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:support-annotations:21.0.+'
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.jakewharton:butterknife:6.+'
    compile 'com.marvinlabs:android-floatinglabel-widgets:1.5.+@aar'
    compile 'com.melnykov:floatingactionbutton:1.1.+'
    compile 'com.nispok:snackbar:2.7.+'
    compile 'com.squareup.okhttp:okhttp:2.1.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.+'
    compile 'com.squareup.retrofit:retrofit:1.8.+'
    compile 'io.reactivex:rxandroid:0.23.1-dev.73+0.x-SNAPSHOT'
    compile 'io.reactivex:rxjava:+'
}
